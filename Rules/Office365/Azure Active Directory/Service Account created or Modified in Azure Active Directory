/*
Date: 14th March 2023
Author: Joe Pointon
Rule Name: Service Account created or Modified in Azure Active Directory
Description:  Identifies when a Service Account has been created or modified in Azure Active Directory.
Alert Level: Medium
Mitre Att&ck Tactic: Persistance
*/

dataset = msft_azure_ad_audit_raw 
| filter activityDisplayName IN ("Add service principal credentials", "Add service principal") and result = "success"
|alter  Username = json_extract(initiatedBy, "$.user.userPrincipalName"), Source_IP = json_extract(initiatedBy, "$.user.ipAddress")
//the following lines parse and extract the information about the target app name
| alter TargetArray = json_extract_array(targetResources, "$.")
| alter TargetApp = arrayindex(TargetArray , 0 )
| alter  Target_App_Name = json_extract(TargetApp , "$.displayName" )
//the following lines parse the actions taken on the secrvice principal
| alter TargetActions = arrayindex(TargetArray, 1)
| alter Actions = json_extract(TargetActions, "$.displayName") 
| fields _time, Username, Source_IP, Target_App_Name as AppName, activityDisplayName as Actions , category, _product as LogSource, result, additionalDetails, initiatedBy, targetResources  
